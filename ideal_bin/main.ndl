link LAN {
    jitter: 0.0,
    latency: 0.005,
    bitrate: 100000000,
}

link WAN {
    jitter: 0.0,
    latency: 0.05,
    bitrate: 1000000000,
}

module EdgeNode {
    gates {
        in @input,
        out @output,
    }
}

module Client: EdgeNode {}
module Server: EdgeNode {}

module Switch {
    gates {
        in[5] @input,
        out[5] @output,

        downlink @input,
        uplink @output,
    }
}

module Router {
    gates {
        switch_in @input,
        switch_out @output,

        wan_in[5] @input,
        wan_out[5] @output,
    }
}

module Network {
    gates {
        in[5],
        out[5],
    }

    submodules {
        node[5]: dyn EdgeNode,
        switch: Switch,
        router: Router
    }

    connections {
        node/out --> LAN --> switch/in,
        node/in <-- LAN <-- switch/out,

        switch/uplink --> router/switch_in,
        switch/downlink <-- router/switch_out,


        router/wan_out --> out,
        router/wan_in <-- in,
    }
}

module Main {
    submodules {
        isp1: Network { node = Client },
        srv1: Network { node = Server}
    }

    connections {
        isp1/out[0] --> WAN --> srv1/in[0],
        isp1/in[0] <-- WAN <-- srv1/out[0],
    }
}

entry Main;